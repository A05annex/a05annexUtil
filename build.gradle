plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
}

repositories {
    mavenCentral()
}

dependencies {
    api 'org.jetbrains:annotations:17.0.0'
    api 'com.googlecode.json-simple:json-simple:1.1.1'

    testImplementation 'junit:junit:4.12'
    testImplementation('org.junit.platform:junit-platform-runner:1.4.2')
    testImplementation('org.junit.jupiter:junit-jupiter-api:5.4.2')
    testImplementation('org.junit.jupiter:junit-jupiter-engine:5.4.2')
}

sourceCompatibility = '1.9'
targetCompatibility = '1.9'
version = '0.9.4'
group = 'org.a05annex'

java {
    withJavadocJar()
    withSourcesJar()
}

test {
    testLogging {
        events 'started', 'passed'
    }
}

artifacts {
    archives jar, javadocJar, sourcesJar
}

signing {
    if (project.hasProperty("signing.keyId") &&
            project.hasProperty("signing.password") &&
            project.hasProperty("signing.secretKeyRingFile") )
    {
        sign configurations.archives
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'a05annexUtil'
            from components.java
            pom {
                name = 'A05annex Utilities'
                description = 'A05annex robotics utilities not dependent on the First FRC code base.'
                url = 'https://github.com/A05annex/a05annexUtil'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'https://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'A05annex'
                        name = 'A05annex'
                        email = 'a05annexrobotics@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/A05annex/a05annexUtil.git'
                    developerConnection = 'scm:git:ssh://github.com/A05annex/a05annexUtil.git'
                    url = 'https://github.com/A05annex/a05annexUtil'
                }
            }
        }
    }
    repositories {
        maven {
            name = "GitHubPackages"
            url = "https://maven.pkg.github.com/A05annex/a05annexUtil"
            credentials {
                username = project.hasProperty("githubActor") ?
                        project.property("githubActor") : System.getenv("GITHUB_ACTOR")
                password = project.hasProperty("githubToken") ?
                        project.property("githubToken") : System.getenv("GITHUB_TOKEN")
            }
        }
    }
}